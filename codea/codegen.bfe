%{
/*
 * vim: filetype=c
 */
#include <stdio.h>
#include <stdlib.h>
#include <assert.h>
#include "treenode.h"
#include "translator.h"

 // TODO: hide instructions and expose minimal interface fro translator.c
 // TODO: expand tree grammar to be able to use less registers
 // TODO: is it a good idea to preassign registers in a traversal prior to the codegen
 // TODO: figure out how to test drf operator
 // TODO: test the empty program
 // TODO: extend the symbol table with values and registers
 // TODO: add a scope attribute to the tree pointing to the symbol table
%}

%start stat
%term OP_ID=1 OP_NUM=2 OP_LEQ=3 OP_DIF=4 OP_AND=5 OP_MUL=6 OP_ADD=7 OP_NOT=8 OP_NEG=9 OP_DRF=10 OP_RET=11 OP_NOP=12

%%

stat:   ret                 # 0
stat:   nop                 # 0

reg:    OP_LEQ(reg, reg)    # 3 # bnode->reg =  next_reg(); cmp_leq(kids[0]->reg, kids[1]->reg, bnode->reg);
reg:    OP_LEQ(num, reg)    # 3 # bnode->reg =  next_reg(); cmp_leq_i(kids[0]->val, kids[1]->reg, bnode->reg);
reg:    OP_LEQ(reg, num)    # 3 # bnode->reg =  next_reg(); cmp_geq_i(kids[1]->val, kids[0]->reg, bnode->reg);
num:    OP_LEQ(num, num)    # 0 # bnode->val = kids[0]->val <= kids[1]->val ? -1 : 0;

reg:    OP_DIF(reg, reg)    # 3 # bnode->reg =  next_reg(); cmp_dif(kids[0]->reg, kids[1]->reg, bnode->reg);
reg:    OP_DIF(num, reg)    # 3 # bnode->reg =  next_reg(); cmp_dif_i(kids[0]->val, kids[1]->reg, bnode->reg);
reg:    OP_DIF(reg, num)    # 3 # bnode->reg =  next_reg(); cmp_dif_i(kids[1]->val, kids[0]->reg, bnode->reg);
num:    OP_DIF(num, num)    # 0 # bnode->val = kids[0]->val != kids[1]->val ? -1 : 0;

reg:    OP_AND(reg, reg)    # 2 # bnode->reg = next_reg(); _mov(kids[0]->reg, bnode->reg); _and(kids[1]->reg, bnode->reg);
reg:    OP_AND(num, reg)    # 2 # bnode->reg = next_reg(); _mov_i(kids[0]->val, bnode->reg); _and(kids[1]->reg, bnode->reg);
reg:    OP_AND(reg, num)    # 2 # bnode->reg = next_reg(); _mov_i(kids[1]->val, bnode->reg); _and(kids[0]->reg, bnode->reg);
num:    OP_AND(num, num)    # 0 # bnode->val = kids[0]->val & kids[1]->val;

reg:    OP_MUL(reg, reg)    # 2 # bnode->reg = next_reg(); _mov(kids[0]->reg, bnode->reg); _mul(kids[1]->reg, bnode->reg);
reg:    OP_MUL(num, reg)    # 2 # bnode->reg = next_reg(); _mov_i(kids[0]->val, bnode->reg); _mul(kids[1]->reg, bnode->reg);
reg:    OP_MUL(reg, num)    # 2 # bnode->reg = next_reg(); _mov_i(kids[1]->val, bnode->reg); _mul(kids[0]->reg, bnode->reg);
num:    OP_MUL(num, num)    # 0 # bnode->val = kids[0]->val * kids[1]->val;

reg:    OP_ADD(reg, reg)    # 2 # bnode->reg = next_reg(); _mov(kids[0]->reg, bnode->reg); _add(kids[1]->reg, bnode->reg); // TODO: replace by leaq (l,r), d
reg:    OP_ADD(num, reg)    # 2 # bnode->reg = next_reg(); _mov_i(kids[0]->val, bnode->reg); _add(kids[1]->reg, bnode->reg); // TODO: replace by leaq (l,r), d
reg:    OP_ADD(reg, num)    # 2 # bnode->reg = next_reg(); _mov_i(kids[1]->val, bnode->reg); _add(kids[0]->reg, bnode->reg); // TODO: replace by leaq (l,r), d
num:    OP_ADD(num, num)    # 0 # bnode->val = kids[0]->val + kids[1]->val;

reg:    OP_NOT(reg)         # 2 # bnode->reg = next_reg(); _mov(kids[0]->reg, bnode->reg); _not(bnode->reg);
num:    OP_NOT(num)         # 0 # bnode->val = ~kids[0]->val;

reg:    OP_NEG(reg)         # 2 # bnode->reg = next_reg(); _mov(kids[0]->reg, bnode->reg); _neg(bnode->reg);
num:    OP_NEG(num)         # 0 # bnode->val = -kids[0]->val;

reg:    OP_DRF(reg)         # 1 # bnode->reg = next_reg(); _drf(kids[0]->reg, bnode->reg);
num:    OP_DRF(num)         # 1 # bnode->reg = next_reg(); _drf_i(kids[0]->val, bnode->reg);

reg:    id                  # 0

ret:    OP_RET(reg)         # 1 # _mov(kids[0]->reg, "rax"); _ret();
ret:    OP_RET(num)         # 1 # _mov_i(kids[0]->val, "rax"); _ret();
ret:    OP_RET(nop)         # 0

num:    OP_NUM              # 0 
id:     OP_ID               # 0 
nop:    OP_NOP              # 0

%%

extern treenode *root;
extern int yyparse(void);
void burm_reduce(NODEPTR_TYPE bnode, int goalnt);

void invoke_burm(NODEPTR_TYPE root) {
        burm_label(root);
        burm_reduce(root, 1);
#ifdef DBG
        print_tree(root);
#endif /* DBG */
}
